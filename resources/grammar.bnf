QUERY =
  VIEW
  | VIEW 'where' CONSTRS
  | VIEW 'with' CONSTRS
  | VIEW 'in which' CONSTRS
  | VIEW 'for which' CONSTRS
  | VIEW 'have' CONSTRS
  | VIEW 'are' CONSTRS
  | VIEW 'is' CONSTRS
  | VIEW 'be' CONSTRS
  | VIEW 'have' CONSTRS
  | VIEW CONSTRS
  | ORGANISM ',' VIEW
  | ORGANISM ',' VIEW CONSTRS
  | ORGANISM 'show the' VIEW
  | ORGANISM 'show the' VIEW CONSTRS
  | VIEW 'in' ORGANISM
  | VIEW CONSTRS 'in' ORGANISM
  | 'the' QUERY
  | 'show' QUERY
  | 'show me' QUERY
  | 'show I' QUERY
  | 'show i' QUERY
  | 'show all' QUERY
  | 'show the' QUERY
  | 'which' QUERY
  | 'all' QUERY
  | 'all the' QUERY
  | 'all of the' QUERY
  | 'for' QUERY
  | 'list' QUERY
  | 'list all' QUERY
  | 'what is' QUERY
  | 'I want to see' QUERY
  | 'i want to see' QUERY
  | "the" QUERY
  | QUERY 'in' ORGANISM
  | ORGANISM QUERY
  | 'from' ORGANISM 'select' QUERY
  | 'from' ORGANISM QUERY
  | QUERY 'sort' SORT
  | QUERY 'sort by' SORT
  | QUERY "?"
  | QUERY "."
  | QUERY "!"


VIEW =
  PATH ('and' PATH)+
  | PATH (',' PATH)+
  | PATH

PATH =
  CLASS
  | CLASS FIELD
  | FIELD
  | FIELD 'of' CLASS
  | CLASS 'with' FIELD
  | (CLASS '.')+ FIELD


CONSTRS =
  CONSTR ('and' CONSTR)+
  | CONSTR ('or' CONSTR)+
  | CONSTR (',' CONSTR)+
  | CONSTR

CONSTR =
  VALUE
  | PATH 'exists'
  | PATH COMPARE VALUE
  | PATH VALUE
  | VALUE PATH
  | PATH MULTI_COMPARE VALUES
  | VALUES MULTI_COMPARE PATH
  | UNARY_OP PATH
  | PATH UNARY_OP


COMPARE =
  STR_COMP
  | MATH_COMP
  | LIST_COMP

STR_COMP =
  EQ
  | NEQ
  | CONTAINS
  | LIKE
  | NLIKE

CONTAINS =
  "contains"
  | "contain"
  | "has"
  | "in"

LIKE =
  "like"
  | "similar to"
  | "resembles"

NLIKE =
  "not" LIKE
  | "isn't" LIKE
  | "be not" LIKE

MATH_COMP =
  EQ
  | NEQ
  | GT
  | LT
  | GEQ
  | LEQ

EQ =
  'EQ'
  | 'equal'
  | 'equal to'
  | 'equals'
  | '='


NEQ =
  'NEQ'
  | 'not equal'
  | 'not equal to'
  | 'doesn\'t equal'
  | 'does not equal'
  | '!='

GT =
  'GT'
  | 'greater'
  | 'greater than'
  | 'over'
  | 'more than'
  | 'above'
  | 'longer'
  | 'longer than'
  | '>'


LT =
  'LT'
  | 'lesser'
  | 'less'
  | 'lesser than'
  | 'less than'
  | 'below'
  | 'under'
  | 'shorter'
  | 'shorter than'
  | '<'

GEQ =
  GT 'or' EQ
  | '>='

LEQ =
  LT 'or' EQ
  | '<='

LIST_COMP =
  "%NOT IMPLEMENTED"

MULTI_COMPARE =
  M_ONE_OF
  | M_NONE_OF
  | M_OVERLAPS
  | M_NOVERLAPS
  | M_OUTSIDE
  | M_WITHIN
  | M_CONTAINS
  | M_NCONTAINS

M_ONE_OF =
  "one of"
  | "one of either"
  | "contained in"
  | "in"
  | "either"

M_NONE_OF =
  "not" M_ONE_OF
  | "isn't" M_ONE_OF
  | "be not" M_ONE_OF
  | "neither"

M_OVERLAPS =
  "between"
  | "in between"

M_NOVERLAPS =
  "not" M_OVERLAPS
  | "isn't" M_OVERLAPS
  | "be not" M_OVERLAPS

M_OUTSIDE =
  "%NOT IMPLEMENTED"

M_WITHIN =
  "%NOT IMPLEMENTED"

M_CONTAINS =
  "%NOT IMPLEMENTED"

M_NCONTAINS =
  "%NOT IMPLEMENTED"

UNARY_OP =
  NULL
  | NNULL

NULL =
  "null"
  | "empty"

NNULL =
  "not" NULL
  | "isn't" NULL
  | "be not" NULL
  | "is not" NULL

SORT =
  SORT SORT
  | VALUE 'descend'
  | VALUE 'ascend'

VALUES =
  VALUE (("or"|"and"|",") VALUE)+


VALUE =
  #"[0-9a-zA-Z]+"

ORGANISM =
  'fly'
  | 'drosophila'
  | 'Drosophila'
